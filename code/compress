#!/usr/bin/guile \
-e main -s
!#

;; Copyright (C) 2013 Daniel Hartwig <mandyke@gmail.com>
;;
;; This program is free software: you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation, either version 3 of the License, or
;; (at your option) any later version.
;;
;; This program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU General Public License for more details.
;;
;; You should have received a copy of the GNU General Public License
;; along with this program.  If not, see <http://www.gnu.org/licenses/>.

(use-modules (lzw)
             (ice-9 control)
             (ice-9 format)
             (ice-9 i18n)
             (rnrs bytevectors)
             (rnrs io ports)
             (srfi srfi-37))

(define *program-name* #f)

;; This form of 'gettext' is helpful for longer messages.  A single
;; message id can be split and aligned across many lines, similar to
;; the common usage in C.
(define (_ msg . rest)
  (gettext (string-concatenate (cons msg rest)) "guile100-compress"))

(define (error* status msg . args)
  (force-output)
  (let ((port (current-error-port)))
    (when *program-name*
      (display *program-name* port)
      (display ": " port))
    (apply format port msg args)
    (newline port)
    (unless (zero? status)
      ;; This call to 'abort' causes 'main' to immediately return the
      ;; specified status value.  Similar to 'exit' but more
      ;; controlled, for example, when using the REPL to debug,
      ;; 'abort' will not cause the entire process to terminate.
      ;;
      ;; This is also handy to attempt processing every file, even
      ;; after an error has occured.  To do this, establish another
      ;; prompt at an interesting place inside 'main'.
      (abort (lambda (k)
               status)))))

(define (make-file-error-handler filename)
  (lambda args
    (error* 1 (_ "~a: ~a")
            filename
            (strerror (system-error-errno args)))))

(define (system-error-handler key subr msg args rest)
  (apply error* 1 msg args))

(define (compression-ratio nbytes-in nbytes-out)
  (exact->inexact (/ (- nbytes-in nbytes-out) nbytes-in)))

(define (write-lzw-header port bits)
  (put-bytevector port (u8-list->bytevector (list #x1F #x9D bits))))

(define (compress-port in out bits verbose?)
  #;
  (begin
    (write-lzw-header out bits)
    (%lzw-compress (cute get-u8 in)
                   (cute put-u16 out <>)
                   eof-object?
                   (expt 2 bits)))
  (let* ((in-bv (get-bytevector-all in))
         (out-bv (lzw-compress in-bv #:table-size (expt 2 bits))))
    (write-lzw-header out bits)
    (put-bytevector out out-bv)))

(define (compress-file infile bits verbose?)
  (catch 'system-error
    (lambda ()
      (let ((outfile (string-append infile ".Z")))
        (when (string-suffix? ".Z" infile)
          (error* 1 (_ "~a: already has .Z suffix") infile))
        (when (file-exists? outfile)
          (error* 1 (_ "~a: already exists") outfile))
        (let ((in (open-file infile "rb"))
              (out (open-file outfile "wb")))
          ;; TODO: Keep original files ownership, modes, and access
          ;; and modification times.
          (compress-port in out bits verbose?)
          (when verbose?
            (format #; (current-error-port)
                    (current-output-port)
                    (_ "~a: compression: ~1,2h%\n") ; '~h' is localized '~f'.
                    infile
                    (* 100 (compression-ratio (port-position in)
                                              (port-position out)))))
          (for-each close-port (list in out))
          (delete-file infile))))
    system-error-handler))

(define (ensure-bits obj)
  (let ((n (or (and (integer? obj) obj)
               (and (string? obj)
                    (locale-string->integer obj))
               (error* 1 (_ "bits must be an integer -- ~a") obj))))
    (unless (<= 9 n 16)
      (error* 1 (_ "bits must be between 9 and 16 -- ~a") n))
    n))

(define (make-boolean-processor key)
  (lambda (opt name arg config . rest)
    (apply values (assq-set! config key #t)
           rest)))

(define (make-option-processor key parse)
  (lambda (opt name arg config . rest)
    (apply values (assq-set! config key (parse arg))
           rest)))

(define (usage status)
  (format (current-error-port)
          (_ "Usage: ~a [-v] [-b bits] [FILE]...\n"
             "    -v, --verbose         show compression ratio\n"
             "    -b, --bits bits       maximum number of BITS per code [16]\n")
          *program-name*)
  (abort (lambda (k)
           status)))

(define options
  (list (option '(#\h "help") #f #f
                (lambda args
                  (usage 0)))
        (option '(#\v "verbose") #f #f
                (make-boolean-processor 'verbose?))
        (option '(#\b "bits") #t #f
                (make-option-processor 'bits ensure-bits))))

(define (main args)
  ;; Establishing this prompt ensures that any call to 'abort' will at
  ;; most escape to the continuation of '%' here.  In effect, calling
  ;; 'abort' causes 'main' to stop what it was doing and continue with
  ;; the procedure passed to 'abort' instead.
  (% (call-with-values
         (lambda ()
           (args-fold (cdr args)
                      options
                      (lambda (opt name arg . rest)
                        (error* 0 (_ "invalid option -- '~a'") name)
                        (usage 1))
                      (lambda (arg config infiles)
                        (values config
                                (cons arg infiles)))
                      ;; First seed: config (with default values).
                      '((bits . 16)
                        (verbose? . #f))
                      ;; Second seed: infiles (initially empty list).
                      '()))
       (lambda (config infiles)
         (let ((bits (assq-ref config 'bits))
               (verbose? (assq-ref config 'verbose?)))
           (for-each (lambda (infile)
                       (cond ((string=? infile "-")
                              (compress-port (current-input-port)
                                             (current-output-port)
                                             bits
                                             verbose?))
                             (else
                              (compress-file infile
                                             bits
                                             verbose?))))
                     (if (null? infiles)
                         ;; No arguments, use stdin.
                         '("-")
                         ;; Process the files in the order given on
                         ;; the command line.
                         (reverse infiles)))
           ;; Exit indicating success.  If an error occured anywhere,
           ;; the call to 'abort' will produce a different status.
           0)))))

(when (batch-mode?)
  (setlocale LC_ALL "")
  (set! *program-name* (basename (car (program-arguments))))
  (exit (main (program-arguments))))
